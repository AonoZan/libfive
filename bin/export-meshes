#!/usr/bin/env -S guile --no-auto-compile -s

This is a headless, minimal version of Studio.

It evaluates a given script (in the same sandboxed environment),
finds every shape, and saves them to an STL file.

TODO: Error handling is non-existent.
!#

(use-modules (libfive lib)
             (libfive kernel)
             (libfive sandbox)
             (libfive vec))

(use-modules (srfi srfi-1)
             (ice-9 match)
             (ice-9 textual-ports))

(match (program-arguments)
  ((_ source-file stl-file)
   (let ((shapes (append-map (match-lambda
                               ((and ('error _ ...)
                                     arg)
                                (error arg))
                               ((_ (? shape? shape))
                                (list shape))
                               (_ '()))
                             (eval-sandboxed (call-with-input-file source-file
                                               get-string-all)))))
     (match global-bounds
       ((xmin ymin zmin xmax ymax zmax)
        (shapes-save-mesh shapes stl-file global-resolution
                          (libfive-region (list xmin xmax)
                                          (list ymin ymax)
                                          (list zmin zmax))
                          global-quality)))))
  ((program _ ...)
   (format #t "Usage: ~A input.io output.stl\n  Headless utility to export meshes from a Studio file\n"
           program)
   (exit 1)))
